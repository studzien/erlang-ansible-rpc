#!/usr/bin/env escript
%%
%%! +sbtu +A0
-module(erlang_rpc).

%% API exports
-export([main/1]).

-record(opts, {node, function, args, cookie, module, nametype}).

%%====================================================================
%% API functions
%%====================================================================
%% escript Entry point
main(Args) ->
    io:format("~s~n", [do(Args)]),
    erlang:halt(0).

%%====================================================================
%% Internal functions
%%====================================================================
do(Opts) ->
    try
        ModuleOpts = read_opts(Opts),
        case check_opts(ModuleOpts) of
            ok ->
                start_network(ModuleOpts),
                Result = make_call(ModuleOpts),
                success(Result);
            {missing, Missing} ->
                failure([io_lib:format("Missing parameters: ~p", [Missing])])
        end
    catch
        Error:Reason ->
            failure([io_lib:format("~p ~p ~p",
                                   [Error, Reason, erlang:get_stacktrace()])])
    end.

start_network(#opts{nametype=NameType, cookie=Cookie} = Opts) ->
    {ok, _Pid} = net_kernel:start([node_name(Opts), NameType]),
    true = erlang:set_cookie(node(), Cookie).

make_call(#opts{node=Node, module=Module, function=Function, args=Args}) ->
    case rpc:call(Node, Module, Function, Args) of
        {badrpc, Reason} -> throw({badrpc, Reason});
        Result -> Result
    end.

%% ansible - from variable file
read_opts([OptsFile]) ->
    {ok, Content} = file:read_file(OptsFile),
    {match, Values} = re:run(Content, <<"([^=]*)=\"([^\"]*)\"\s*">>,
                             [global, {capture, [1,2], binary}]),
    ValueToOpt = fun([<<"node">>, Node], Opts) ->
                         Opts#opts{node = atom(Node)};
                    ([<<"function">>, Function], Opts) ->
                         Opts#opts{function = atom(Function)};
                    ([<<"args">>, Args], Opts) ->
                         Opts#opts{args = parse_args(fix_arguments_opt(Args))};
                    ([<<"cookie">>, Cookie], Opts) ->
                         Opts#opts{cookie = atom(Cookie)};
                    ([<<"module">>, Module], Opts) ->
                         Opts#opts{module = atom(Module)};
                    ([<<"nametype">>, NameType], Opts) ->
                         Opts#opts{nametype = atom(NameType)}
                 end,
    lists:foldl(ValueToOpt, #opts{}, Values);
%% docker - from arguments
read_opts([Node, Cookie, NameType, Module, Function, Args]) ->
    #opts{node = atom(Node),
          cookie = atom(Cookie),
          nametype = atom(NameType),
          module = atom(Module),
          function = atom(Function),
          args = parse_args(Args)}.

check_opts(#opts{}=Opts) ->
    [opts|Values] = tuple_to_list(Opts),
    Fields = record_info(fields, opts),
    CheckOpt = fun({Name, undefined}) -> {true, Name};
                  (_) -> false
               end,
    case lists:filtermap(CheckOpt, lists:zip(Fields, Values)) of
        [] -> ok;
        Missing -> {missing, Missing}
    end.

fix_arguments_opt(Args) ->
    {match, [Match]} = re:run(Args, <<"'(.*)'">>, [{capture, [1], binary}]),
    binary_to_list(Match).

parse_args(String) ->
    {ok, Tokens, _} = erl_scan:string(String ++ "."),
    {ok, Args} = erl_parse:parse_term(Tokens),
    Args.

success(Result) ->
    BinaryResult = iolist_to_binary(io_lib:format("~p", [Result])),
    EscapedResult = re:replace(BinaryResult, <<"\"">>, <<"\\\\\"">>, [global]),
    [<<"{\"result\": \" ">>, EscapedResult, <<"\"}">>].

failure(Msg) ->
    [<<"{\"failed\": true, \"msg\": \"">>, iolist_to_binary(Msg), <<"\"}">>].

node_name(#opts{node = Node}) ->
    {ok, Hostname} = inet:gethostname(),
    list_to_atom("rpc-caller-" ++ atom_to_list(Node) ++ "@" ++ Hostname).

atom(List) when is_list(List) ->
    list_to_atom(List);
atom(Binary) when is_binary(Binary) ->
    binary_to_atom(Binary, utf8).

%% vim: ft=erlang
